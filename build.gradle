plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

configurations {
    embed
    compile.extendsFrom(embed)
}

group 'net.okocraft.respawner'
version '1.0.1-SNAPSHOT'

sourceCompatibility = 10

repositories {
    mavenCentral()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    embed 'com.github.hazae41:mc-kutils:master-SNAPSHOT'

    compileOnly "org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT"
}

compileKotlin {
    kotlinOptions.jvmTarget = "10"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "10"
}

bukkit {
    main        = "net.okocraft.respawner.Respawner"
    apiVersion  = "1.14"

    name = 'Respawner'
    description = "Respawner, to revive dead player"
    website = "https://github.com/okocraft/Respawner"
    author = "akaregi"

    commands {
        respawn {
            aliases = ["rs"]
            permission = "respawner.respawn"
        }
    }

    permissions {
        "respawner.*" {
            children = ["respawner.respawn"]
        }

        "respawner.respawn" {
            description = "Allows you to run /respawn"
            setDefault("OP")
        }
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}